module generate

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.model.annotations.GeneratedClassAnnotation
import org.eclipse.emf.mwe2.ecore.EcoreGenerator

// This variable is adapted by the Maven build
var workspaceRoot = "../../../"
var rootPath = "${workspaceRoot}/bundles/dsls/"
var testRootPath = "${workspaceRoot}/tests/dsls"
var baseName = "tools.vitruv.dsls.commonalities"
var basePath = "${rootPath}/${baseName}"
var testBasePath = "${testRootPath}/${baseName}"
var languageElementsModel = "platform:/resource/${baseName}/model/LanguageElements.genmodel"

Workflow {
 	bean = tools.vitruv.dsls.common.elements.RemapEcoreModel {
		from = "http://www.eclipse.org/emf/2002/Ecore"
		to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtime = {
					ecoreModel = "${basePath}/model-gen"
				}
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}

		language = StandardLanguage {
			name = "tools.vitruv.dsls.commonalities.CommonalitiesLanguage"
			fileExtensions = "commonality"
			referencedResource = languageElementsModel
			junitSupport = {
				junitVersion = "5"
			}
			referencedResource = "platform:/resource/tools.vitruv.dsls.common/model/CommonLanguageElements.genmodel"
		}
	}

	component = EcoreGenerator {
		srcPath = "platform:/resource/${baseName}/src"
		genModel = languageElementsModel
		generateCustomClasses = false
	}

}
