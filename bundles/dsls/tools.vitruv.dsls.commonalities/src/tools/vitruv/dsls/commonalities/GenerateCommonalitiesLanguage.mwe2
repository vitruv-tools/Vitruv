module tools.vitruv.dsls.commonalities.GenerateCommonalitiesLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.model.annotations.GeneratedClassAnnotation
import org.eclipse.emf.mwe2.ecore.EcoreGenerator
import org.eclipse.emf.mwe.utils.StandaloneSetup
import org.eclipse.emf.mwe.utils.DirectoryCleaner

var rootPath = ".."
var testRootPath = "${rootPath}/../../tests/dsls"
var baseName = "tools.vitruv.dsls.commonalities"
var basePath = "${rootPath}/${baseName}"
var testBasePath = "${testRootPath}/${baseName}"
var languageElementsModel = "platform:/resource/tools.vitruv.dsls.commonalities/model/LanguageElements.genmodel"

Workflow {

	component = DirectoryCleaner {
		directory = "${basePath}/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${basePath}/model-gen"
	}

	component = DirectoryCleaner {
		directory = "${basePath}.ui/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${basePath}.ide/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${testBasePath}.tests/src-gen"
	}

	component = DirectoryCleaner {
		directory = "${testBasePath}.ui.tests/src-gen"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtime = {
					ecoreModel = "${basePath}/model-gen"
				}
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = false
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}

		language = StandardLanguage {
			name = "tools.vitruv.dsls.commonalities.CommonalitiesLanguage"
			fileExtensions = "commonality"
			referencedResource = languageElementsModel
			// not used as of now:
			//referencedResource = "platform:/resource/tools.vitruv.dsls.mirbase/model/generated/MirBase.genmodel"
		}
	}

	component = EcoreGenerator {
		srcPath = "platform:/resource/tools.vitruv.dsls.commonalities/src"
		genModel = languageElementsModel
		generateCustomClasses = false
	}
}
