grammar tools.vitruv.dsls.commonalities.CommonalitiesLanguage
	with org.eclipse.xtext.xbase.Xbase
	hidden(WS, ML_COMMENT, SL_COMMENT)

generate language "http://vitruv.tools/dsls/commonalities/language"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://vitruv.tools/dsls/commonalities/elements"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

CommonalityFile:
	(operatorImports+=OperatorImport)*
	concept=Concept
	commonality=Commonality
;

OperatorImport:
	'import' (importedType=[jvmTypes::JvmDeclaredType|QualifiedName] | importedNamespace=QualifiedNameWithWildcard)
;

Domain:
	Concept
;

Concept:
	'concept' name=ValidID
;

Metaclass:
	Commonality
;

Commonality:
	(referenced?='referenced')? 'commonality' name=ValidID '{'
		participations+=Participation*
		(attributes+=CommonalityAttribute|references+=CommonalityReference)*
	'}'
;



/******************
 * Participations *
 ******************/

PackageLike:
	Participation
;

Participation:
	'with' (SimpleParticipation | TupleParticipation)
	('as' Aliasable)?
	('whereat' '{'
		(
			conditions+=ParticipationCondition
			(',' conditions+=ParticipationCondition)*
		)?
	'}')?
;

SimpleParticipation:
	participationClass=QualifiedParticipationClassDeclaration
;

TupleParticipation:
	domainName=DomainName ':' '(' parts+=TupleParticipationPart (','
	parts+=TupleParticipationPart)* ')'
;

TupleParticipationPart:
	SimpleTupleParticipationPart | ParticipationRelation
;

SimpleTupleParticipationPart:
	participationClass=UnqualifiedParticipationClassDeclaration
;

ParticipationRelation:
	SimpleParticipationRelation | BracedParticipationRelation
;

SimpleParticipationRelation returns ParticipationRelation:
	leftClasses+=UnqualifiedParticipationClassDeclaration operator=ParticipationRelationOperator
	rightClasses+=UnqualifiedParticipationClassDeclaration
;

BracedParticipationRelation returns ParticipationRelation:
	'(' leftClasses+=UnqualifiedParticipationClassDeclaration (',' leftClasses+=UnqualifiedParticipationClassDeclaration)*
	operator=ParticipationRelationOperator
	rightClasses+=UnqualifiedParticipationClassDeclaration (',' rightClasses+=UnqualifiedParticipationClassDeclaration)*
	')'
;

ParticipationRelationOperator:
	jvmType=[jvmTypes::JvmDeclaredType|OperatorName]
;

fragment Aliasable:
	alias=STRING
;

ClassLike:
	ParticipationClass
;

ParticipationClass:
	QualifiedParticipationClassDeclaration | UnqualifiedParticipationClassDeclaration
;

QualifiedParticipationClassDeclaration returns ParticipationClass:
	(singleton?='single')? superMetaclass=[Metaclass|QualifiedClass] ('called' Aliasable)?
;

UnqualifiedParticipationClassDeclaration returns ParticipationClass:
	(singleton?='single')? superMetaclass=[Metaclass|UnqualifiedClass] ('called' Aliasable)?
;

ParticipationCondition:
	({BidirectionalParticipationCondition} | '->' {EnforcedParticipationCondition} | '<-' {CheckedParticipationCondition})
	leftOperand=ParticipationConditionLeftOperand operator=ParticipationConditionOperator
	(
		(rightOperands+=ParticipationConditionRightOperand)
		| ('(' (rightOperands+=ParticipationConditionRightOperand (',' rightOperands+=ParticipationConditionRightOperand)*)? ')')
	)?
;

ParticipationConditionOperator:
	jvmType=[jvmTypes::JvmDeclaredType|OperatorName]
;

ParticipationConditionLeftOperand returns ParticipationConditionOperand:
	UnqualifiedParticipationClassOperand | UnqualifiedParticipationAttributeOperand
;

ParticipationConditionRightOperand returns ParticipationConditionOperand:
	UnqualifiedParticipationClassOperand | UnqualifiedParticipationAttributeOperand | LiteralOperand
;

ParticipationConditionOperand:
	UnqualifiedParticipationClassOperand | UnqualifiedParticipationAttributeOperand | LiteralOperand
;

/**************
 * Attributes *
 **************/

Attribute:
	CommonalityAttribute | ParticipationAttribute | CommonalityReference
;

CommonalityAttribute:
	'has' name=ValidID '{'
		mappings+=CommonalityAttributeMapping*
	'}'
;

CommonalityAttributeMapping:
	SimpleAttributeMapping | OperatorAttributeMapping
;

SimpleAttributeMapping:
	(readAndWrite?='=' | write?='->'| read?='<-')
	attribute=QualifiedParticipationAttribute
;

OperatorAttributeMapping:
	(readAndWrite?='=' | write?='->'| read?='<-')
	operator=AttributeMappingOperator '(' (operands+=AttributeMappingOperand (',' operands+=AttributeMappingOperand)*)? ')'
;

AttributeMappingOperator:
	jvmType=[jvmTypes::JvmDeclaredType|OperatorName]
;

AttributeMappingOperand:
	LocalUnqualifiedCommonalityAttributeOperand | QualifiedParticipationAttributeOperand | QualifiedParticipationClassOperand | LiteralOperand
;

ParticipationAttribute:
	QualifiedParticipationAttribute | UnqualifiedParticipationAttribute
;

QualifiedParticipationAttribute returns ParticipationAttribute:
	participationClass=[ParticipationClass|QualifiedClass] '.' attribute=[Attribute|UnqualifiedAttribute]
;

UnqualifiedParticipationAttribute returns ParticipationAttribute:
	participationClass=[ParticipationClass|UnqualifiedClass] '.' attribute=[Attribute|UnqualifiedAttribute]
;

CommonalityAttributeReference:
	LocalUnqualifiedCommonalityAttributeReference
;

// Local: Can only refer to the (single) local commonality.
LocalUnqualifiedCommonalityAttributeReference returns CommonalityAttributeReference:
	commonality=[Commonality|UnqualifiedClass] '.' attribute=[CommonalityAttribute|UnqualifiedAttribute]
;

/**************
 * References *
 **************/

CommonalityReference:
	'has' name=ValidID 'referencing' referenceType=[Commonality|QualifiedClass] '{'
		mappings+=CommonalityReferenceMapping*
	'}'
;

CommonalityReferenceMapping:
	SimpleReferenceMapping | OperatorReferenceMapping
;

SimpleReferenceMapping:
	(readAndWrite?='=' | write?='->'| read?='<-')
	reference=QualifiedParticipationAttribute
	// TODO not used right now
	('via' referenceTargets+=[ParticipationClass|QualifiedClass] (',' referenceTargets+=[ParticipationClass|QualifiedClass])*)?
;

OperatorReferenceMapping:
	(readAndWrite?='=' | write?='->'| read?='<-')
	participationClass=[ParticipationClass|QualifiedClass] '.' operator=ReferenceMappingOperator
	'(' (operands+=ReferenceMappingOperand (',' operands+=ReferenceMappingOperand)*)? ')'
;

ReferenceMappingOperator:
	jvmType=[jvmTypes::JvmDeclaredType|OperatorName]
;

ReferenceMappingOperand:
	ReferencedParticipationAttributeOperand | UnqualifiedParticipationAttributeOperand | LiteralOperand
;

ReferencedParticipationAttributeOperand:
	'ref' participationAttribute=UnqualifiedParticipationAttribute
;

/************
 * Operands *
 ************/

Operand:
	ParticipationConditionOperand | AttributeMappingOperand | ReferenceMappingOperand
;

LiteralOperand:
	expression=(XBooleanLiteral |
		XNumberLiteral |
		XNullLiteral |
		XStringLiteral)
	// TODO List literal
;

QualifiedParticipationClassOperand returns ParticipationClassOperand:
	participationClass=[ParticipationClass|QualifiedClass]
;

UnqualifiedParticipationClassOperand returns ParticipationClassOperand:
	participationClass=[ParticipationClass|UnqualifiedClass]
;

QualifiedParticipationAttributeOperand returns ParticipationAttributeOperand:
	participationAttribute=QualifiedParticipationAttribute
;

UnqualifiedParticipationAttributeOperand returns ParticipationAttributeOperand:
	participationAttribute=UnqualifiedParticipationAttribute
;

LocalUnqualifiedCommonalityAttributeOperand returns CommonalityAttributeOperand:
	attributeReference=LocalUnqualifiedCommonalityAttributeReference
;

/******************
 * Datatype Rules *
 ******************/

DomainName:
	ValidID
;

UnqualifiedClass:
	ValidID
;

QualifiedClass:
	DomainName ':' UnqualifiedClass
;

UnqualifiedAttribute:
	ValidID
;

// To correctly parse character combinations, we need to copy all tokens that
// already occurred in the parent grammars (i.e. Xbase).
// '->' and '<-': Ambiguous since the next participation condition may start with this as well
OperatorName:
	ID |
	( '+=' | '-=' | '*=' | '/=' | '%=' | '>=' | '<=' | '||' | '&&' | '==' | '!=' | '===' | '!=='
	| '=>' | '<>' | '?:' | '**' | '--' | '++' | '::'
	| '=' | '!' | '%' | '&' | '*' | '+' | '-' | '/' | '<' | '>' | '^' | '~' | '_' | ':' | '?')+
;

/******************
 * Terminal Rules *
 ******************/

