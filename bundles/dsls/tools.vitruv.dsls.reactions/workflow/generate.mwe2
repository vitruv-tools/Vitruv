module generate

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe2.ecore.EcoreGenerator

var rootPath = ".."
var testRootPath = "${rootPath}/../../tests/dsls"
var projectName = "tools.vitruv.dsls.reactions"
var testBasePath = "${testRootPath}/${projectName}"

Workflow {

    bean = tools.vitruv.dsls.common.elements.RemapEcoreModel {
		from = "http://www.eclipse.org/emf/2002/Ecore"
		to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "tools.vitruv.dsls.reactions.ReactionsLanguage"
			fileExtensions = "reactions"
			junitSupport = junit.JUnitFragment {
				junitVersion = "5"
				generateStub = false
			}
			formatter = {
				generateStub = true
			}
			referencedResource = "platform:/resource/tools.vitruv.dsls.common/model/CommonLanguageElements.genmodel"
			referencedResource = "platform:/resource/tools.vitruv.dsls.reactions/model/TopLevelElements.genmodel"
		}
	}
	
	component = EcoreGenerator {
		srcPath = "platform:/resource/tools.vitruv.dsls.reactions/src"
		genModel = "platform:/resource/tools.vitruv.dsls.reactions/model/TopLevelElements.genmodel"
		generateCustomClasses = false
	}
	
	component = EcoreGenerator {
		srcPath = "platform:/resource/tools.vitruv.dsls.reactions/src"
		genModel = "platform:/resource/tools.vitruv.dsls.reactions/model/InputTypes.genmodel"
		generateCustomClasses = false
	}

}
