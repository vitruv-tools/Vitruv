module generate

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.StandaloneSetup

var projectName = "tools.vitruv.dsls.mappings"
var languageName = "MappingsLanguage"
var fileExtension = "mappings"

var qualifiedLanguageName = "${projectName}.${languageName}"
var rootPath = ".."
var testRootPath = "${rootPath}/../../tests/dsls"
var testBasePath = "${testRootPath}/${projectName}"

Workflow {

	bean = tools.vitruv.dsls.common.elements.RemapEcoreModel {
		from = "http://www.eclipse.org/emf/2002/Ecore"
		to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
	}
	
	bean = StandaloneSetup {
    	scanClassPath = true
    	// The following two lines can be removed, if Xbase is not used.
    	registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
    	registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
    	registerGenModelFile = "platform:/resource/tools.vitruv.dsls.common/model/CommonLanguageElements.genmodel"
    	registerGenModelFile = "platform:/resource/tools.vitruv.dsls.reactions/model/generated/ReactionsLanguage.genmodel"
    }

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = true
				
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}

		language = StandardLanguage {			
			name = qualifiedLanguageName
			fileExtensions = fileExtension
			junitSupport = junit.JUnitFragment {
				generateStub = false
			}
			referencedResource = "platform:/resource/tools.vitruv.dsls.common/model/CommonLanguageElements.genmodel"
			referencedResource = "platform:/resource/tools.vitruv.dsls.reactions/model/generated/ReactionsLanguage.genmodel"
		}
	}

}
