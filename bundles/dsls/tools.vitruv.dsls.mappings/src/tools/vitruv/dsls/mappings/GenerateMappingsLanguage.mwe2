module tools.vitruv.dsls.mappings.GenerateMappingsLanguage

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

// TODO MK: refactor commonalities with GenerateReactionsLanguage.mwe2 if possible
var projectName = "tools.vitruv.dsls.mappings"
var languageName = "MappingsLanguage"
var fileExtension = "mappings"

var qualifiedLanguageName = "${projectName}.${languageName}"
var rootPath = ".."
var testRootPath = "${rootPath}/../../tests/dsls"
var basePath = "${rootPath}/${projectName}"
var testBasePath = "${testRootPath}/${projectName}"

Workflow {
	bean = StandaloneSetup {
    	scanClassPath = true
    	// The following two lines can be removed, if Xbase is not used.
    	registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
    	registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
    	registerGenModelFile = "platform:/resource/tools.vitruv.dsls.mirbase/model/generated/MirBase.genmodel"
    	registerGenModelFile = "platform:/resource/tools.vitruv.dsls.reactions/model/generated/ReactionsLanguage.genmodel"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}/model/generated"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}.ui/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${basePath}.ide/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${testBasePath}.tests/src-gen"
    }
    
    component = DirectoryCleaner {
    	directory = "${testBasePath}.ui.tests/src-gen"
    }
    
	component = postprocessor.MappingsLanguageExtendedGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = projectName
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${testBasePath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testBasePath}.ui.tests"
				}
				createEclipseMetaData = true
				
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {			
			name = qualifiedLanguageName
			fileExtensions = fileExtension
			junitSupport = junit.JUnitFragment {
				generateStub = false
			}
			referencedResource = "platform:/resource/tools.vitruv.dsls.mirbase/model/generated/MirBase.genmodel"
			referencedResource = "platform:/resource/tools.vitruv.dsls.reactions/model/generated/ReactionsLanguage.genmodel"
			
		}
	}
}
